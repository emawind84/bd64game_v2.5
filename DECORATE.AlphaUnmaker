ACTOR LasergunSpawner : CustomInventory 9351
{
//+INVENTORY.ALWAYSPICKUP														//R12.1
Inventory.PickupMessage "You got the Experimental Laser Gun"
Inventory.PickupSound "WPICK"
Scale 0.9
	States
	{
	Spawn:
		LGUN B 1
		LGUN B -1
		Stop
	Pickup:
		LGUN B 1
		TNT1 A 0 A_GiveInventory("Lasergun")
		Stop
	}
}

actor Lasergun : DoomWeapon
{
//$Category Weapons
spawnid 84
game Doom
scale 0.82
Weapon.SelectionOrder 200														//WAS 2900
Inventory.PickupMessage "You got the Experimental Laser Gun"
AttackSound "weapons/AlphaLZRGker"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Weapon.AmmoType "Cell"
Inventory.Icon LGUNB0															//R27 WAS LGUNA0
Tag "Laser Gun"																	//R11 ADDED TAG
	States
	{
	Spawn:
		LGUN B -1
		stop
	Ready:
		LZRG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")					//R28.2
		TNT1 A 0 A_TakeInventory("Stamina", 0)									//R28.2
		loop
	Select:
		TNT1 AAAAA 1 A_Raise
		TNT1 AAAAAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		LZRS ABCD 1
		TNT1 A 0 A_giveInventory("HasPLasmaWeapon", 1)
		Goto Ready
	Deselect:
		LZRS DCBA 1
		TNT1 A 0 A_TakeInventory("HasPLasmaWeapon", 1)
		TNT1 A 0 A_StopSoundEx("WEAPON")
		TNT1 AAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_ZoomFactor(0.99)
		TNT1 A 0 A_PlaySound ("LSSTART",2)
	Hold:
		TNT1 A 0 A_PlaySoundEx ("LSLOOP","WEAPON",1)
		LZRG B 1 BRIGHT A_FireCustomMissile("AlphaLZRGkerLaser", 0, 1)
		LZRG C 1 BRIGHT A_FireCustomMissile("AlphaLZRGkerLaser", 0, 0)
		LZRG B 1 BRIGHT A_FireCustomMissile("AlphaLZRGkerLaser", 0, 1)
		LZRG C 1 BRIGHT A_FireCustomMissile("AlphaLZRGkerLaser", 0, 0)
		TNT1 A 0 A_Refire
		TNT1 A 0 A_StopSoundEx("WEAPON")
		TNT1 A 0 A_PlaySound ("LSSTOP",2)
		TNT1 A 0 A_ZoomFactor(1.0)
		LZRG AAAAA 2 A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0)
		Goto Ready
	DoKick:
		TNT1 A 0
		KICK A 0 A_Takeinventory("Kicking",1)
		TNT1 A 0 A_JumpIfInventory("IsModern", 1, "ModernCheck")				//R28
		TNT1 A 0 A_JumpIfInventory("istacticalclass", 1, 1)						//R7 ADDED
		Goto Ready																//R7 ADDED
		TNT1 A 0 A_jumpifinventory("Stamina",20,4)
		TNT1 A 0 A_Print("Not enough stamina!", 1)
		TNT1 A 0 A_PlaySound("Tired", 2)
		Goto Ready
		TNT1 AAA 0
		TNT1 A 0 A_TakeInventory("Stamina", 20)
//		TNT1 A 0 A_Takeinventory("Zoomed",1)									//R11 UNUSED
	ModernCheck:																//R28 ADDED
		TNT1 A 0 A_ZoomFactor(1.0)
//		TNT1 A 0 A_Takeinventory("ADSmode",1)									//R11 UNUSED
		TNT1 A 0 A_JumpIf (momZ > 0, "AirKick")
		TNT1 A 0 A_JumpIf (momZ < 0, "AirKick")
		TNT1 A 0 A_JumpIf (pitch > 32, "LowKickChecker1")
		Goto InitializeNormalKick
	LowKickChecker1:
		TNT1 A 0
		TNT1 A 0 A_JumpIf (pitch > 90, "InitializeNormalKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned", 0, 0, 0, 0)
	InitializeNormalKick:
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"BerserkerKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack", 0, 0, 0, -7)
		KICK H 5
		KICK A 0 A_Takeinventory("Kicking",1)
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto SelectAnimation
	BerserkerKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("SuperKickAttack", 0, 0, 0, -7)
		KICK H 5
		KICK A 0 A_Takeinventory("Kicking",1)
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto SelectAnimation
	AirKick:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("Stamina", 5)									//R17 WAS 20
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"SuperAirKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil (-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("AirKickAttack", 0, 0, 0, -31)
		KICK O 3
		KICK A 0 A_Takeinventory("Kicking",1)
		KICK PQRST 2
		Goto SelectAnimation
	SuperAirKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil (-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("SuperAirKickAttack", 0, 0, 0, -31)
		KICK O 3
		KICK A 0 A_Takeinventory("Kicking",1)
		KICK PQRST 2
		Goto SelectAnimation
	}
}

actor AlphaLZRGkerLaser : FastProjectile 9954									//R1 WAS 9904
{
game Doom
radius 1
height 1
speed 999
scale 0.6
damage 2
PROJECTILE
+RANDOMIZE
+EXTREMEDEATH
+FORCERADIUSDMG
DAmagetype "Plasma"
Renderstyle Add
Decal Bulletchip
Alpha 1
SeeSound "weapons/AlphaLZRGker"
DeathSound "Null"
Obituary "%o was fried by %k's laser."											//r30.10 was LZRGde by %k.
	States
	{
	Spawn:
		TNT1 A 1
		loop
	Death:
		TNT1 AAAAAAAA 0 A_CustomMissile ("ExplosionParticleLaser", 0, 0, random (0, 360), 2, random (0, 180))
		TNT1 AAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 AA 1 A_SpawnItem("RocketFlare")
		TNT1 A 0 A_SpawnItemEx ("DetectFloorBullet",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)	//R28.2
		TNT1 A 0 A_SpawnItemEx ("DetectCeilBullet",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)		//R28.2
		//EXPL A 0 A_CustomMissile ("ExplosionSmokeFast22", 5, 0, random (0, 360), 2, random (0, 360))
		Stop
	}
}

actor AlphaLZRGkerLaserTrail
{
game Doom
height 4
radius 2
scale 0.6
speed 0
damage 0
Renderstyle Add
Alpha 1
seesound "null"
deathsound "null"
PROJECTILE
+RANDOMIZE
	states
	{
	Spawn:
		LAZR abab 1 bright
		stop
	Death:
		LPUF A 1 bright
		stop
	}
}